/**
* @File Name          : fsGenericUploadDocumentsController.cls
* @Description        : Service Class For fsGenericUploadDocuments Lightning Component
* @Author             : Yogendra Degra
* @Created On         : 20/07/2022
*==============================================================================
* Ver         Date                     Author                 Modification
*==============================================================================
* 1.0         20/07/2022              Yogendra Degra       Initial Version
*/
public Without Sharing class fsGenericUploadDocumentsController {
    
    @AuraEnabled
    public static List<ContentVersion> getUploadedRecords(String parentId){
        return [SELECT Id, Document_Name__c, Is_Additional_Document__c, Title, ContentDocumentId, OwnerId, Owner.Name, Number_of_Pages__c, Remarks__c, Uploaded_From__c FROM ContentVersion WHERE Is_Additional_Document__c = FALSE AND Parent_Id__c =: parentId];
    }
    @AuraEnabled
    public static List<ContentVersion> getAdditionalRecords(String parentId){
        return [SELECT Id, Document_Name__c, Is_Additional_Document__c, Title, ContentDocumentId, OwnerId, Owner.Name, Number_of_Pages__c, Remarks__c, Uploaded_From__c FROM ContentVersion WHERE Is_Additional_Document__c = true AND Parent_Id__c =: parentId];
    }
    @AuraEnabled
    public static WrapperData getAllRequiredData(String stageName, String applicationId, String recordTypeId){
        WrapperData wrapper = new WrapperData();
        Set<String> setCodeIds = new Set<String>();
        Map<String, String> mapOfDeferalIds = new Map<String, String>();
        /**
         * Get Master Records
         * Get Applicant Records
         * Get Asset Records
         */
        wrapper.listOfDocumentMaster = [SELECT Id, Name, Description__c, Type__c, Active__c, Family__c FROM Document_Master__c];
        wrapper.listOfDocumentSetCode = [SELECT Id, Name, Type__c, Applicable_For__c, Income_Type__c, Asset_Type__c, Sub_Type__c, Mandatory__c, Stage__c, Active__c, Family__c, Document_Master__c, Document_Master__r.Name, Document_Master__r.Active__c FROM Document_Set_Code__c WHERE Stage__c =: stageName AND Active__c =: 'Yes' AND Document_Master__r.Active__c =: 'Yes'];
        wrapper.listOfLoanApplicant = [SELECT Id, Customer_Information__c, Customer_Type__c, Application__c, Applicant_Name__c FROM Loan_Applicant__c WHERE Application__c =: applicationId];
        wrapper.listOfProperty = [SELECT Id, Name, Property__c, Property_Type__c  FROM Property__c WHERE Application__c =: applicationId AND recordTypeId =:recordTypeId AND Property_Type__c != ''];

        /**
         * Get Document Deferral Uploaded Records
         */

        for(Deferral_Document__c def : [SELECT Id, Deferred_From__c, Document_Family__c, Is_Document_Received__c, Document_Name__c, Mandatory__c, Type__c, Current_Record_Id__c, Document_Set_Code__c, Deferral_Stage__c, Status__c, Deferral_Date__c, Remarks__c, Waiver_Reason__c, Uploaded_From__c, Is_Deferral_Document__c, Is_Document_Master_Record__c, Parent_Property_Id__c, Applicable_For__c, Applicant_Name__c, Applicant_Type__c FROM Deferral_Document__c WHERE Document_Set_Code__c != '' AND Application__c =: applicationId]){
            if(def.Deferral_Stage__c == stageName && def.Is_Deferral_Document__c && !def.Is_Document_Master_Record__c){
                mapOfDeferalIds.put(def.Document_Set_Code__c, def.Id);
            }
            if(def.Is_Document_Master_Record__c){
                wrapper.listOfDeferralMasterDocumentDocument.add(def);
            }
            if(def.Type__c == 'Application'){
                wrapper.mapOfDeferralDocument.put(def.Document_Set_Code__c, def);
            } else if(def.Type__c == 'Applicant'){
                String uniqueId = def.Document_Set_Code__c + '-' + def.Current_Record_Id__c;
                wrapper.mapOfDeferralDocument.put(uniqueId, def);
            } else if(def.Type__c == 'Asset'){
                String setCodeWithPropertyId = def.Document_Set_Code__c + def.Current_Record_Id__c;
                String setCodeWithParentProperty = def.Document_Set_Code__c + def.Parent_Property_Id__c;
                wrapper.mapOfDeferralDocument.put(setCodeWithPropertyId + '-' + setCodeWithParentProperty, def);
            }
        }
        for(Document_Set_Code__c objSet : [SELECT Id, Name, Type__c, Applicable_For__c, Income_Type__c, Asset_Type__c, Sub_Type__c, Mandatory__c, Stage__c, Active__c, Family__c, Document_Master__c, Document_Master__r.Active__c FROM Document_Set_Code__c WHERE Id IN :mapOfDeferalIds.keySet()]){
            String defId = mapOfDeferalIds.get(objSet.Id);
            wrapper.mapOfDeferralSetCode.put(defId, objSet);    
        }
        
        /**
         * Get CV Uploaded Records
         */
        for(ContentVersion cv : [SELECT Id, Parent_Property_Id__c, Current_Stage__c, Deferred_From__c, Parent_Id__c, Document_Name__c, Is_Document_Master_Record__c, ContentDocumentId, Mandatory__c, Uploaded_From__c, Current_Record_Id__c, Document_Type__c, Document_Set_Code_Id__c, Received_Date__c, Remarks__c, Number_of_Pages__c, Original__c FROM ContentVersion WHERE Parent_Id__c =: applicationId AND Uploaded_From__c =: stageName AND (Document_Set_Code_Id__c != '' OR Is_Document_Master_Record__c = True)]){
            if(cv.Document_Type__c == 'Application'){
                wrapper.mapOfContentVersion.put(cv.Document_Set_Code_Id__c, cv);
            }else if(cv.Document_Type__c == 'Applicant'){
                wrapper.mapOfContentVersion.put(cv.Document_Set_Code_Id__c + '-' + cv.Current_Record_Id__c, cv);
            } else if(cv.Document_Type__c == 'Asset'){
                String setCodeWithPropertyId = cv.Document_Set_Code_Id__c + cv.Current_Record_Id__c;
                String setCodeWithParentProperty = cv.Document_Set_Code_Id__c + cv.Parent_Property_Id__c;
                wrapper.mapOfContentVersion.put(setCodeWithPropertyId + '-' + setCodeWithParentProperty, cv);
            }   
        }
        /**
         * Get All Employment Records
         */
        Set<String> setOfLoanApplicantId = new Set<String>();
        for(Loan_Applicant__c objLoan : wrapper.listOfLoanApplicant){
            if(String.isNotBlank(objLoan.Customer_Information__c)){
                setOfLoanApplicantId.add(objLoan.Id);    
            }
        }
        wrapper.listOfEmpoymentDetails = [SELECT Id, Name, Application__c, Occupation__c, Customer_Information__c, Loan_Applicant__c FROM Employment_Details__c WHERE Loan_Applicant__c =: setOfLoanApplicantId];
        return wrapper;
    }
    @AuraEnabled
    public static WrapperUploadedDocument getUploadedData(String stageName, String applicationId){
        WrapperUploadedDocument objWrapper = new WrapperUploadedDocument();
        objWrapper.listOfContentVersion = [SELECT Id,  Uploaded_From__c, Parent_Id__c, Document_Name__c, Current_Record_Id__c, Document_Type__c FROM ContentVersion WHERE Parent_Id__c =: applicationId AND Uploaded_From__c =: stageName];
        objWrapper.listOfDeferralDocument = [SELECT Id, Application__c, Is_Deferral_Document__c, Current_Record_Id__c, Type__c, Status__c, Document_Set_Code__c FROM Deferral_Document__c  WHERE Application__c =: applicationId AND Uploaded_From__c =: stageName];
        return objWrapper;
    }
    public class WrapperUploadedDocument{
        @AuraEnabled
        public List<ContentVersion> listOfContentVersion;
        @AuraEnabled
        public List<Deferral_Document__c> listOfDeferralDocument;
        public WrapperUploadedDocument(){
            listOfContentVersion = new List<ContentVersion>();
            listOfDeferralDocument = new List<Deferral_Document__c>();
        }
    }
    public class WrapperData{
        @AuraEnabled
        public List<Document_Master__c> listOfDocumentMaster; 
        @AuraEnabled
        public Map<String, Document_Set_Code__c> mapOfDeferralSetCode; 
        @AuraEnabled
        public List<Document_Set_Code__c> listOfDocumentSetCode; 
        @AuraEnabled
        public List<Loan_Applicant__c> listOfLoanApplicant; 
        @AuraEnabled
        public List<Property__c> listOfProperty; 
        @AuraEnabled
        public List<Employment_Details__c> listOfEmpoymentDetails; 
        @AuraEnabled
        public Map<String, ContentVersion> mapOfContentVersion; 
        @AuraEnabled
        public Map<String, Deferral_Document__c> mapOfDeferralDocument; 
        @AuraEnabled
        public List<Deferral_Document__c> listOfDeferralMasterDocumentDocument; 

        public WrapperData(){
            listOfDocumentMaster = new List<Document_Master__c>();
            mapOfDeferralSetCode = new Map<String, Document_Set_Code__c>();
            listOfDocumentSetCode = new List<Document_Set_Code__c>(); 
            listOfLoanApplicant = new List<Loan_Applicant__c>(); 
            listOfProperty = new List<Property__c>(); 
            listOfEmpoymentDetails = new List<Employment_Details__c>();
            mapOfContentVersion = new Map<String, ContentVersion>();
            mapOfDeferralDocument = new Map<String, Deferral_Document__c>();
            listOfDeferralMasterDocumentDocument = new List<Deferral_Document__c>();
        }
    }
    
    //Documents Save Process
    @AuraEnabled
    public static ResponseWrapper createCVRecord(String data, String currentStageName){
        System.debug('data :: '+data);
        WrapperCV jsonWrapper = (WrapperCV)JSON.deserialize(data, WrapperCV.class);
        System.debug('jsonWrapper :: '+jsonWrapper);
        Set<String> setOfDefDelRecords = new Set<String>();
        ContentVersion cv = new ContentVersion();
        cv.Title = jsonWrapper.fileData.filename;
        cv.VersionData = EncodingUtil.base64Decode(jsonWrapper.fileData.base64);
        cv.PathOnClient = jsonWrapper.fileData.filename;
        if(String.isNotBlank(jsonWrapper.deferalRecordId)){
            setOfDefDelRecords.add(jsonWrapper.deferalRecordId);
        } else{
            cv.Current_Stage__c = currentStageName;
            cv.Deferred_From__c = currentStageName;
        }
        if(!jsonWrapper.isNewRowAdded && !jsonWrapper.isDocumentMaster){
            cv.Document_Set_Code_Id__c = jsonWrapper.docSetCodeId; 
        } else{
            cv.Is_Document_Master_Record__c = true;
            cv.Document_Set_Code_Id__c = jsonWrapper.docSetCodeId;
        }
        if(String.isNotBlank(jsonWrapper.parentPropertyId)){
            cv.Parent_Property_Id__c = jsonWrapper.parentPropertyId;
        }
        
        cv.Document_Name__c = jsonWrapper.documentName;
        cv.Document_Family__c = jsonWrapper.documentFamily;
        cv.Document_Type__c = jsonWrapper.documentType;
        cv.Document_Number__c = jsonWrapper.documentCode;
        if(cv.Document_Type__c == 'Applicant'){
            cv.Current_Record_Id__c = jsonWrapper.applicantId;
        }
        if(cv.Document_Type__c == 'Asset'){
            cv.Current_Record_Id__c = jsonWrapper.propertyId;
        }
        cv.Document_Condition__c = jsonWrapper.documentCondition;
        cv.Agreement_Document_Type__c = jsonWrapper.agreementDocumentType;
        cv.Mandatory__c = jsonWrapper.mandatory;
        cv.Status__c = jsonWrapper.status;
        cv.Uploaded_From__c = currentStageName;
        cv.Original__c = jsonWrapper.original;
        cv.Remarks__c = jsonWrapper.remarks;
        cv.Received_Date__c = jsonWrapper.receivedDate;
        cv.Parent_Id__c = jsonWrapper.fileData.recordId;
        cv.OwnerId = UserInfo.getUserId();
        cv.Number_of_Pages__c = jsonWrapper.noOfPages;
        System.debug(':: cv :: '+cv);
        insert cv;  
        createContentLink(cv.Id, jsonWrapper.fileData.recordId);
        //ContentDocumentLink cdl = createContentLink(cv.Id, jsonWrapper.fileData.recordId);
		if(jsonWrapper.isDeferalRecord && String.isNotBlank(jsonWrapper.deferalRecordId)){
            Deferral_Document__c objDef = new Deferral_Document__c();
            objDef.Id = jsonWrapper.deferalRecordId;
            objDef.Is_Deferral_Document__c = false;
            objDef.Is_Document_Received__c = true;
            update objDef;
        }
        /*if(setOfDefDelRecords != null && setOfDefDelRecords.size() > 0){
            DELETE [SELECT Id FROM Deferral_Document__c WHERE Id IN: setOfDefDelRecords];
        }*/
        ResponseWrapper response = new ResponseWrapper();
        response.deferalRecordId = '';
        return response;
    }
    @AuraEnabled
    public static ResponseWrapper createDeferralRecord(String data, String currentStageName, String recordId){
        System.debug('recordId ##### '+recordId);
        DeferralWrapper jsonWrapper = (DeferralWrapper)JSON.deserialize(data, DeferralWrapper.class);  
        Set<String> setOfDefSetCodeRecords = new Set<String>();
        Deferral_Document__c objDef = new Deferral_Document__c();
        if(String.isNotBlank(jsonWrapper.deferalRecordId)){
            objDef.Id = jsonWrapper.deferalRecordId;
        }else{
            objDef.Deferred_From__c = currentStageName;
        }
        objDef.Application__c = recordId; 
        objDef.Document_Name__c = jsonWrapper.documentName; 
        objDef.Document_Family__c = jsonWrapper.documentFamily; 
        objDef.Status__c = jsonWrapper.status;
        objDef.Mandatory__c = jsonWrapper.mandatory; 
        objDef.Type__c = jsonWrapper.documentType; 
        objDef.Uploaded_From__c = currentStageName;
        if(String.isNotBlank(jsonWrapper.docSetCodeId)){
            setOfDefSetCodeRecords.add(jsonWrapper.docSetCodeId);
        }
        /*if(jsonWrapper.isNewRowAdded){
            objDef.Deferred_From__c = currentStageName;
        }*/
        objDef.Applicable_For__c = jsonWrapper.applicableFor;
        objDef.Applicant_Name__c = jsonWrapper.customerType;
        objDef.Applicant_Type__c = jsonWrapper.customerName;
        if(String.isNotBlank(jsonWrapper.parentPropertyId)){
            objDef.Parent_Property_Id__c = jsonWrapper.parentPropertyId;
        }
        if(!jsonWrapper.isNewRowAdded && !jsonWrapper.isDocumentMaster){
            objDef.Document_Set_Code__c = jsonWrapper.docSetCodeId; 
        } else{
            objDef.Is_Document_Master_Record__c = true;
            objDef.Document_Set_Code__c = jsonWrapper.docSetCodeId;
        }
        if(jsonWrapper.documentType == 'Applicant'){
            objDef.Current_Record_Id__c = jsonWrapper.applicantId;
        }
        if(jsonWrapper.documentType == 'Asset'){
            objDef.Current_Record_Id__c = jsonWrapper.propertyId;
        }
        if(jsonWrapper.status == 'Waived'){
            objDef.Waiver_Reason__c = jsonWrapper.waiverReason;
            objDef.Remarks__c = jsonWrapper.remarks;
        }
        if(jsonWrapper.status == 'Deferred'){
            objDef.Is_Deferral_Document__c = true;    
            objDef.Deferral_Stage__c = jsonWrapper.stage;
            objDef.Deferral_Date__c = jsonWrapper.deferredDate;
            objDef.Remarks__c = jsonWrapper.remarks;
        }
        System.debug('objDef ##### '+objDef);
        upsert objDef;
        ResponseWrapper response = new ResponseWrapper();
        response.deferalRecordId = objDef.Id;

        if(setOfDefSetCodeRecords != null && setOfDefSetCodeRecords.size() > 0){
            Set<String> setOfContentDocument = new Set<String>();
            if(jsonWrapper.documentType == 'Application'){
                for(ContentVersion cv : [SELECT Id, ContentDocumentId FROM ContentVersion WHERE Document_Set_Code_Id__c IN: setOfDefSetCodeRecords]){
                    setOfContentDocument.add(cv.ContentDocumentId);
                }
            } else{
                for(ContentVersion cv : [SELECT Id, ContentDocumentId FROM ContentVersion WHERE Document_Set_Code_Id__c IN: setOfDefSetCodeRecords AND Current_Record_Id__c =: objDef.Current_Record_Id__c]){
                    setOfContentDocument.add(cv.ContentDocumentId);
                }
            }

            if(setOfContentDocument != null && setOfContentDocument.size() > 0) {
                DELETE [SELECT Id FROM ContentDocument WHERE Id IN: setOfContentDocument];
            }
            
        }
        return response;
    }
    
    
    private static void createContentLink(String cvId, String recordId) {
        //if (cvId == null || recordId == null) { return null; }
        List<ContentDocumentLink> listOfCDL = new List<ContentDocumentLink>();
        ContentDocumentLink cdl = new ContentDocumentLink();
        cdl.ContentDocumentId = [SELECT ContentDocumentId FROM ContentVersion WHERE Id =: cvId].ContentDocumentId;
        cdl.LinkedEntityId = recordId;
        cdl.ShareType = 'I';
        //listOfCDL.add(cdl);
        
        ContentDocumentLink cdl2 = new ContentDocumentLink();
        cdl2.ContentDocumentId = [SELECT ContentDocumentId FROM ContentVersion WHERE Id =: cvId].ContentDocumentId;
        cdl2.LinkedEntityId = UserInfo.getOrganizationId();
        cdl2.ShareType = 'V';
        listOfCDL.add(cdl2);
        insert listOfCDL;
    }
    public class ResponseWrapper{
        @AuraEnabled public String deferalRecordId;
        @AuraEnabled public String contentDocumentId;
        @AuraEnabled public String contentVersionId;
        public ResponseWrapper(){
            deferalRecordId = '';
            contentDocumentId = '';
            contentVersionId = '';
        }
    }
    public class WrapperCV{
        @AuraEnabled public String documentCode;
        @AuraEnabled public String docSetCodeId;
        @AuraEnabled public String serialNumber;
        @AuraEnabled public String parentPropertyId;
        @AuraEnabled public String applicantId;
        @AuraEnabled public String applicableFor;
        @AuraEnabled public String customerType;
        @AuraEnabled public String customerName;
        @AuraEnabled public String documentName;
        @AuraEnabled public String documentFamily;
        @AuraEnabled public String documentType;
        @AuraEnabled public String agreementDocumentType;
        @AuraEnabled public String documentCondition;
        @AuraEnabled public String mandatory;
        @AuraEnabled public String status;
        @AuraEnabled public String stage;
        @AuraEnabled public Date deferredDate;
        @AuraEnabled public String waiverReason;
        @AuraEnabled public Date receivedDate;
        @AuraEnabled public String noOfPages;
        @AuraEnabled public String remarks;
        @AuraEnabled public Boolean isNewRowAdded;
        @AuraEnabled public String fileName;
        @AuraEnabled public String original;
        @AuraEnabled public String propertyId;
        @AuraEnabled public Boolean isDeferalRecord;
        @AuraEnabled public String deferalRecordId;
        @AuraEnabled public String contentDocumentId;
        @AuraEnabled public Boolean isDocumentMaster;
        @AuraEnabled public fileData fileData;
    }
    public class fileData {
        @AuraEnabled
        public String filename; 
        @AuraEnabled
        public String base64;   
        @AuraEnabled
        public String recordId;
    }
    public class DeferralWrapper{
        @AuraEnabled public Integer serialNumber;   
        @AuraEnabled public string docSetCodeId;
        @AuraEnabled public String documentName;    
        @AuraEnabled public String documentFamily; 
        @AuraEnabled public String parentPropertyId; 
        @AuraEnabled public String applicableFor;
        @AuraEnabled public String customerType;
        @AuraEnabled public String customerName;
        @AuraEnabled public String mandatory;   
        @AuraEnabled public String documentType;
        @AuraEnabled public String status;  
        @AuraEnabled public String stage;   
        @AuraEnabled public boolean deferredStageDisable;
        @AuraEnabled public boolean deferredRequired;
        @AuraEnabled public date deferredDate;
        @AuraEnabled public date receivedDate;
        @AuraEnabled public boolean isReceivedDateRequired;
        @AuraEnabled public boolean receivedDateDisable;
        @AuraEnabled public String noOfPages;   
        @AuraEnabled public String waiverReason;
        @AuraEnabled public boolean isWaiverRequired;
        @AuraEnabled public boolean waiverReasonDisable;
        @AuraEnabled public String remarks; 
        @AuraEnabled public Boolean isNewRowAdded;  
        @AuraEnabled public String fileName;    
        @AuraEnabled public boolean isFileUploadDisabled;
        @AuraEnabled public boolean isFileUploadRequired;
        @AuraEnabled public String original;    
        @AuraEnabled public boolean isoriginalDisabled;
        @AuraEnabled public String fileData;    
        @AuraEnabled public boolean isAgreementExecution;
        @AuraEnabled public String applicationId;
        @AuraEnabled public String applicantId;
        @AuraEnabled public String propertyId;  
        @AuraEnabled public Boolean isDeferalRecord;
        @AuraEnabled public String deferalRecordId;
        @AuraEnabled public String contentDocumentId;
        @AuraEnabled public Boolean isDocumentMaster;
    }
    //Additional Document Functionality
    @AuraEnabled
    public static void uploadAddtionalDocument(String base64, String filename, String recordId, String description) {
        ContentVersion cv = new ContentVersion();
        cv.VersionData = EncodingUtil.base64Decode(base64);
        cv.Title = filename;
        cv.PathOnClient = filename;
        cv.Document_Name__c = filename;
        cv.Remarks__c = description;
        cv.Parent_Id__c = recordId;
        cv.OwnerId = UserInfo.getUserId();
        cv.Is_Additional_Document__c = true;
        insert cv;
		createContentLink(cv.Id, recordId);
        //ContentDocumentLink cdl = createContentLink(cv.Id, recordId);
    }
}